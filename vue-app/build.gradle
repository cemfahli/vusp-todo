buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    } 
  }

  dependencies {
    classpath "com.moowork.gradle:gradle-node-plugin:1.3.1"
  }
}

apply plugin: 'base'
apply plugin: 'com.moowork.node'

node {
  version = '10.14.1'
  npmVersion = '6.4.1'
  download = true
}

npm_run_build {
  inputs.files fileTree("public")
  inputs.files fileTree("src")

  inputs.file 'package.json'
  inputs.file 'package-lock.json'

  outputs.dir 'dist'
}

task packageVueApp(type: Zip) {
  dependsOn npm_run_build
  baseName 'vue-app'
  extension 'jar'
  destinationDirectory = file("${projectDir}/build_packageVueApp")
  from('dist') {
    into 'static'
  }
}

configurations {
  vueResources
}

configurations.default.extendsFrom(configurations.vueResources)

artifacts {
  vueResources(packageVueApp.archivePath) {
    builtBy packageVueApp
    type "jar"
  }
}

assemble.dependsOn packageVueApp

clean {
  delete packageVueApp.archivePath
}